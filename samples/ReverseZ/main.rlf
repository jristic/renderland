
ObjImport {
	ObjPath = "scene.obj";
} sceneObj

Buffer {
	Flags = { Vertex };
	InitData = sceneObj.vertices;
} verts

Buffer {
	Flags = { Index };
	InitData = sceneObj.indices;
} indices


Sampler {
	Filter = { All = Point };
	AddressMode = { UVW=Clamp };
} PointSampler

Texture {
	Format = R8G8B8A8_UNORM;
	Size = {1024,1024};
	Flags = { RTV, SRV };
} RT

Texture {
	Format = D16_UNORM;
	Size = {1024,1024};
	Flags = { DSV };
} DS


Draw {
	Topology = TriList;
	VShader = VertexShader {
		ShaderPath = "shader.hlsl";
		EntryPoint = "VSMain";
	};
	PShader = PixelShader {
		ShaderPath = "shader.hlsl";
		EntryPoint = "PSMain";
	};
	VertexBuffer = verts;
	IndexBuffer = indices;
	RenderTarget = RT;
	DepthStencil = DS;
	DSState = DepthStencilState {
		DepthFunc = Greater;
	};
	SetConstantVs Matrix = 
		projection(3.14/2, DisplaySize().x / float(DisplaySize().y), 10, 0.1)
		* 
		lookAt(float3(3+sin(time()),1,0), float3(0,0.5,0));
} obj_draw

Dispatch {
	Shader = ComputeShader {
		ShaderPath = "shader.hlsl";
		EntryPoint = "CSMain";
	};
	ThreadPerPixel = true; 
	Bind OutTexture = @BackBuffer;
	Bind InTexture = RT;
	Bind Sampler= PointSampler;
	setconstant TextureSize = DisplaySize();
} copy

Passes {
	ClearColor{ Target=RT; Color={0,0,0,1}; },
	ClearDepth{ Target=DS; Depth=0; },
	obj_draw,
	copy
}
