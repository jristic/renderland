
ObjImport {
	ObjPath = "../../common/monkey.obj";
} monkeyObj

Buffer {
	Flags = { Vertex };
	InitData = monkeyObj.vertices;
} verts

Buffer {
	Flags = { Index };
	InitData = monkeyObj.indices;
} indices


Sampler {
	Filter = { All = Point };
	AddressMode = { UVW=Clamp };
} PointSampler

Texture {
	Format = R8G8B8A8_UNORM;
	Size = {1024,1024};
	Flags = { RTV, SRV };
} RT

Texture {
	Format = D24_UNORM_S8_UINT;
	Size = {1024,1024};
	Flags = { DSV };
} DS


Draw {
	Topology = TriList;
	VShader = VertexShader {
		ShaderPath = "vertex.hlsl";
		EntryPoint = "VSMain";
	};
	VertexBuffer = verts;
	IndexBuffer = indices;
	DepthStencil = DS;
	DSState = DepthStencilState {
		StencilEnable = true;
		FrontFace = {
			StencilPassOp = Replace;
		};
	};
	StencilRef = 1;
	SetConstantVs Matrix = 
		projection(3.14/2, DisplaySize().x / float(DisplaySize().y), 0.1, 10)
		* 
		lookAt(float3(1.5,0,0), float3(0,0,0));
} objDraw

Draw {
	VShader = VertexShader {
		ShaderPath = "other.hlsl";
		EntryPoint = "VSMain";
	};
	PShader = PixelShader {
		ShaderPath = "other.hlsl";
		EntryPoint = "PSMain";
	};
	VertexCount = 3;
	RenderTarget = RT;
	DepthStencil = DS;
	DSState = DepthStencilState {
		DepthEnable = false;
		StencilEnable = true;
		FrontFace = {
			StencilFunc = Equal;
		};
	};
	StencilRef = 1;
} simpleDraw

Dispatch {
	Shader = ComputeShader {
		ShaderPath = "CopyTexture.hlsl";
		EntryPoint = "CSMain";
	};
	ThreadPerPixel = true; 
	Bind OutTexture = @BackBuffer;
	Bind InTexture = RT;
	Bind Sampler= PointSampler;
	setconstant TextureSize = DisplaySize();
} copy

Passes {
	ClearColor{ Target=RT; Color={0,0,0,1}; },
	ClearDepth{ Target=DS; Depth=1; },
	ClearStencil{ Target=DS; Stencil=0; },
	objDraw,
	simpleDraw,
	copy
}
