
ObjImport {
	ObjPath = "monkey.obj";
} monkeyObj

Buffer {
	Flags = { Vertex };
	InitData = monkeyObj.vertices;
} verts

Buffer {
	Flags = { Index };
	InitData = monkeyObj.indices;
} indices


Sampler {
	Filter = { All = Point };
	AddressMode = { UVW=Clamp };
} PointSampler

Texture {
	Format = R8G8B8A8_UNORM;
	Size = {1024,1024};
	Flags = { RTV, SRV };
} RT

Texture {
	Format = D24_UNORM_S8_UINT;
	Size = {1024,1024};
	Flags = { DSV };
} DSV


DrawIndexed {
	Topology = TriList;
	VShader = VertexShader {
		ShaderPath = "vertex.hlsl";
		EntryPoint = "VSMain";
	};
	PShader = PixelShader {
		ShaderPath = "pixel.hlsl";
		EntryPoint = "PSMain";
	};
	VertexBuffer = verts;
	IndexBuffer = indices;
	RenderTarget = RT;
	DepthStencil = DSV;
	DSState = DepthStencilState {
		StencilEnable = true;
		FrontFace = {
			StencilPassOp = Replace;
		};
	};
	StencilRef = 1;
	SetConstantVs Matrix = 
		projection(3.14/2, DisplaySize().x / float(DisplaySize().y), 0.1, 10)
		* 
		lookAt(float3(1.5,0,0), float3(0,0,0));
} objDraw

Dispatch {
	Shader = ComputeShader {
		ShaderPath = "CopyTexture.hlsl";
		EntryPoint = "CSMain";
	};
	ThreadPerPixel = true; 
	Bind OutTexture = @BackBuffer;
	Bind InTexture = RT;
	Bind Sampler= PointSampler;
	setconstant TextureSize = DisplaySize();
} copy

Passes {
	ClearColor{ Target=RT; Color={0,0,0,1}; },
	ClearDepth{ Target=DSV; Depth=1; },
	ClearStencil{ Target=DSV; Stencil=0; },
	objDraw,
	copy
}
